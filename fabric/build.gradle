plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // SimpleVoiceChat dependencies
    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
        "min_minecraft_version": min_mc_version,
        "voicechat_api_version": voicechat_api_version,
        "min_architectury_version": min_architectury_version,
        "fabric_loader_version": fabric_loader_version

    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar

    if (minecraft_version == min_mc_version) {
        archiveAppendix.set("fabric-${minecraft_version}")
    } else {
        archiveAppendix.set("fabric-${min_mc_version}-${minecraft_version}")
    }

    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

curseforge {
    apiKey = file("${rootDir}/curseforge_api_key.txt").exists() ? file("${rootDir}/curseforge_api_key.txt").text : ''
    project {
        id = "825621"
        changelogType = 'markdown'
        changelog = file("${rootDir}/changelog.md")
        releaseType = release_type
        addGameVersion minecraft_version
        addGameVersion 'Fabric'
        mainArtifact(remapJar) {
            if (minecraft_version == min_mc_version) {
                displayName = "[FABRIC][${minecraft_version}] Walkie-Talkie ${mod_version}"
            } else {
                displayName = "[FABRIC][${min_mc_version}-${minecraft_version}] Walkie-Talkie ${mod_version}"
            }
            relations {
                requiredDependency 'simple-voice-chat'
                requiredDependency 'architectury-api'
                requiredDependency 'fabric-api'
            }
        }
    }
}

tasks.modrinth.configure({
    group = 'upload'
})

modrinth {
    token = file("${rootDir}/modrinth_token.txt").exists() ? file("${rootDir}/modrinth_token.txt").text : ''
    projectId = "yaRitiLw"
    versionNumber = "fabric-${mod_version}"
    if (minecraft_version == min_mc_version) {
        versionName = "[FABRIC][${minecraft_version}] Walkie-Talkie ${mod_version}"
    } else {
        versionName = "[FABRIC][${min_mc_version}-${minecraft_version}] Walkie-Talkie ${mod_version}"
    }
    uploadFile = remapJar
    versionType = release_type.toUpperCase()
    changelog = file("${rootDir}/changelog.md").text
    gameVersions = [minecraft_version]
    loaders = ['fabric']
    dependencies {
        required.project '9eGKb6K1' // simple-voice-chat
        required.project 'lhGA9TYQ' // architectury-api
        required.project 'P7dR8mSH' // fabric-api
    }
}
tasks.modrinth.dependsOn(build)

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}